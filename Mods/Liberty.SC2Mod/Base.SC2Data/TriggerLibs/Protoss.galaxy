//==================================================================================================
//  Protoss Melee AI
//==================================================================================================

include "TriggerLibs/Protoss0"
include "TriggerLibs/Protoss1"
include "TriggerLibs/Protoss2"
include "TriggerLibs/Protoss3"
include "TriggerLibs/Protoss4"
include "TriggerLibs/Protoss5"

//--------------------------------------------------------------------------------------------------
//  Counter-Attack Units
//--------------------------------------------------------------------------------------------------
static void InitCounters (int player) {
    // versus Protoss
    AICounterUnitSetup(player, c_PU_Zealot,             1.00, c_PU_Zealot,      1.00, c_PU_Zealot);
    AICounterUnitSetup(player, c_PU_Stalker,            1.00, c_PU_Stalker,     0.80, c_PU_Immortal);
    AICounterUnitSetup(player, c_PU_Sentry,             1.00, c_PU_Zealot,      1.00, c_PU_Zealot);
    AICounterUnitSetup(player, c_PU_HighTemplar,        1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_PU_DarkTemplar,        1.00, c_PU_Zealot,      1.00, c_PU_Zealot);
    AICounterUnitSetup(player, c_PU_Archon,             2.00, c_PU_Zealot,      1.00, c_PU_Carrier);
    AICounterUnitSetup(player, c_PU_Immortal,           2.00, c_PU_Zealot,      2.00, c_PU_Zealot);
    AICounterUnitSetup(player, c_PU_Colossus,           1.00, c_PU_Immortal,    1.00, c_PU_Immortal);
    AICounterUnitSetup(player, c_PU_Phoenix,            1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_PU_VoidRay,            1.50, c_PU_Stalker,     1.50, c_PU_Stalker);
    AICounterUnitSetup(player, c_PU_Carrier,            2.00, c_PU_VoidRay,     2.00, c_PU_VoidRay);
    AICounterUnitSetup(player, c_PB_PhotonCannon,       3.00, c_PU_Zealot,      0.80, c_PU_Carrier);

    // versus Terran
    AICounterUnitSetup(player, c_TU_Marine,             0.80, c_PU_Zealot,      0.20, c_PU_Colossus);
    AICounterUnitSetup(player, c_TU_Marauder,           2.00, c_PU_Zealot,      0.70, c_PU_Immortal);
    AICounterUnitSetup(player, c_TU_Reaper,             1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_TU_Ghost,              2.00, c_PU_Zealot,      2.00, c_PU_Zealot);
    AICounterUnitSetup(player, c_TU_Hellion,            1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_TU_SiegeTank_Alias,    0.60, c_PU_Immortal,    0.60, c_PU_Immortal);
    AICounterUnitSetup(player, c_TU_Thor,               5.00, c_PU_Zealot,      5.00, c_PU_Zealot);
    AICounterUnitSetup(player, c_TU_Viking_Alias,       1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_TU_Medivac,            1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_TU_Raven,              1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_TU_Banshee,            0.70, c_PU_Phoenix,     0.70, c_PU_Phoenix);
    AICounterUnitSetup(player, c_TU_Battlecruiser,      2.00, c_PU_VoidRay,     2.00, c_PU_VoidRay);
    AICounterUnitSetup(player, c_TB_Bunker,             4.00, c_PU_Zealot,      0.80, c_PU_Carrier);

    // versus Zerg
    AICounterUnitSetup(player, c_ZU_Zergling,           0.50, c_PU_Zealot,      0.50, c_PU_Zealot);
    AICounterUnitSetup(player, c_ZU_Baneling,           0.50, c_PU_Stalker,     0.25, c_PU_Immortal);
    AICounterUnitSetup(player, c_ZU_Roach,              1.00, c_PU_Zealot,      0.40, c_PU_Immortal);
    AICounterUnitSetup(player, c_ZU_Hydralisk,          1.00, c_PU_Zealot,      0.30, c_PU_Colossus);
    AICounterUnitSetup(player, c_ZU_Infestor,           2.00, c_PU_Stalker,     2.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_ZU_Mutalisk,           1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_ZU_Corruptor,          1.00, c_PU_Stalker,     1.00, c_PU_Stalker);
    AICounterUnitSetup(player, c_ZU_Ultralisk,          1.20, c_PU_Immortal,    1.20, c_PU_Immortal);
    AICounterUnitSetup(player, c_ZU_BroodLord,          1.00, c_PU_Phoenix,     1.00, c_PU_Phoenix);
    AICounterUnitSetup(player, c_ZB_SpineCrawler,       3.00, c_PU_Zealot,      0.80, c_PU_VoidRay);
}

//--------------------------------------------------------------------------------------------------
//  ProtossSubStateName
//--------------------------------------------------------------------------------------------------
string ProtossSubStateName (int state) {
    // TODO Call the individual difficulties to return a real substate name
    return "-" + IntToString(state) + "-";
}

//--------------------------------------------------------------------------------------------------
//  DebugCallbackProt
//--------------------------------------------------------------------------------------------------
void DebugCallbackProt (int player) {
    DebugAI("=====PROTOSS=====\n");
    DebugMelee(player);
    DebugAI("e_mainState = "        + MainStateName(AIState(player, e_mainState))           +
            ", e_mainSubState = "   + ProtossSubStateName(AIState(player, e_mainSubState))  +
            ", e_attackState = "    + AttackStateName(AIState(player, e_attackState))
    );
}

//--------------------------------------------------------------------------------------------------
//  AINewUnitProt
//--------------------------------------------------------------------------------------------------
void AINewUnitProt (int player, unit u) {
    int obs;
    string type = UnitGetType(u);

    // ignored units
    //
    if (UnitTypeTestAttribute(type, c_unitAttributeStructure)) {
        return;
    }
    if (UnitTypeTestFlag(type, c_unitFlagWorker)) {
        return;
    }
    
    if (type == c_PU_WarpPrism) {
        AIAddToExtraScoutGroup(player, u);
        return;
    }
    
    // detector
    //
    if (type == c_PU_Observer) {
        AINewDetector(player, u, true);
        return;
    }

    // clear obstacle units
    //
    if (AIWaveNeedClearObsUnits(player)) {
        if (type == c_PU_Zealot || type == c_PU_Stalker || type == c_PU_DarkTemplar) {
            AIMergeUnit(player, u, AIWaveGet(player, c_waveClearObs));
            return;
        }
    }

    // main wave units
    //
    AINewUnitDefault(player, u);
}

//--------------------------------------------------------------------------------------------------
//  AIGetScoutProt
//--------------------------------------------------------------------------------------------------
unit AIGetScoutProt (int player, int index, unit prev, bool allowWorkers) {
    unit obs;
    
    if (!AIGetFlag(player, e_flagsScouting)) {
        return c_nullUnit;
    }
    if (UnitGetType(prev) == c_PU_Observer) {
        return prev;
    }
    obs = AIGrabUnit(player, c_PU_Observer, c_prioScout, null);
    if (obs) {
        return obs;
    }
    if (prev) {
        return prev;
    }
    // back to allowing workers as late scouts
    //if (AIGetFlag(player, e_flagsLateScout)) {
    //    return c_nullUnit;
    //}
    if (!allowWorkers) {
        return c_nullUnit;
    }
    return AIGrabUnit(player, c_PU_Probe, c_prioScout, null);
}

//--------------------------------------------------------------------------------------------------
//  AIEarlyDefScoutProt
//--------------------------------------------------------------------------------------------------
unit AIEarlyDefScoutProt (int player, unit prev, bool allowWorkers) {
    unit obs;
    string prevType;
    
    if (!AIGetFlag(player, e_flagsEarlyDefScout)) {
        return c_nullUnit;
    }
    prevType = UnitGetType(prev);
    if (prevType == c_PU_Observer) {
        return prev;
    }
    obs = AIGrabUnit(player, c_PU_Observer, c_prioScout, null);
    if (obs) {
        return obs;
    }
    if (prevType == c_PU_Zealot) {
        return prev;
    }
    obs = AIGrabUnit(player, c_PU_Zealot, c_prioScout, null);
    if (obs) {
        return obs;
    }
    if (prev) {
        return prev;
    }
    if (!allowWorkers) {
        return c_nullUnit;
    }
    return AIGrabUnit(player, c_PU_Probe, c_prioScout, null);
}

//--------------------------------------------------------------------------------------------------
//  AIWaveThinkProt
//--------------------------------------------------------------------------------------------------
void AIWaveThinkProt (int player, wave w, int type) {
    AIWaveThinkDefault(player, w, type);
}

//--------------------------------------------------------------------------------------------------
//  Protoss Init
//--------------------------------------------------------------------------------------------------
static void ProtossInit (int player) {
    int state;

    InitCounters(player);
    AISetNumScouts(player, 1);
    AISetFlag(player, e_flagsScouting, false);
    AISetFlag(player, e_flagsEarlyDefScout, false);
    AISetPowerBuilding(player, c_PB_Pylon);
    AITransportSetPanic(player, 0.27); // just below max shield threshold

    if (AIGetGroundStartLocs(PlayerStartLocation(player)) > 0) {
        state = AIDiffEnum(player, e_mainState_OpenGnd0);
    }
    else {
        state = AIDiffEnum(player, e_mainState_OpenAir0);
    }
    AISetMainState(player, state, e_mainSubState_Unset);
}

//--------------------------------------------------------------------------------------------------
//  AIMeleeProt
//--------------------------------------------------------------------------------------------------
void AIMeleeProt (int player) {    
    int mainState = AIState(player, e_mainState);

    if (mainState == e_mainState_Init)              { ProtossInit(player);     }

    else if (mainState == e_mainState_OpenGnd0)     { ProtossOpenGnd0(player); }
    else if (mainState == e_mainState_OpenGnd1)     { ProtossOpenGnd1(player); }
    else if (mainState == e_mainState_OpenGnd2)     { ProtossOpenGnd2(player); }
    else if (mainState == e_mainState_OpenGnd3)     { ProtossOpenGnd3(player); }
    else if (mainState == e_mainState_OpenGnd4)     { ProtossOpenGnd4(player); }
    else if (mainState == e_mainState_OpenGnd5)     { ProtossOpenGnd5(player); }

    else if (mainState == e_mainState_OpenAir0)     { ProtossOpenAir0(player); }
    else if (mainState == e_mainState_OpenAir1)     { ProtossOpenAir1(player); }
    else if (mainState == e_mainState_OpenAir2)     { ProtossOpenAir2(player); }
    else if (mainState == e_mainState_OpenAir3)     { ProtossOpenAir3(player); }
    else if (mainState == e_mainState_OpenAir4)     { ProtossOpenAir4(player); }
    else if (mainState == e_mainState_OpenAir5)     { ProtossOpenAir5(player); }

    else if (mainState == e_mainState_Mid0)         { ProtossMid0(player);     }
    else if (mainState == e_mainState_Mid1)         { ProtossMid1(player);     }
    else if (mainState == e_mainState_Mid2)         { ProtossMid2(player);     }
    else if (mainState == e_mainState_Mid3)         { ProtossMid3(player);     }
    else if (mainState == e_mainState_Mid4)         { ProtossMid4(player);     }
    else if (mainState == e_mainState_Mid5)         { ProtossMid5(player);     }

    else if (mainState == e_mainState_Late0)        { ProtossLate0(player);    }
    else if (mainState == e_mainState_Late1)        { ProtossLate1(player);    }
    else if (mainState == e_mainState_Late2)        { ProtossLate2(player);    }
    else if (mainState == e_mainState_Late3)        { ProtossLate3(player);    }
    else if (mainState == e_mainState_Late4)        { ProtossLate4(player);    }
    else if (mainState == e_mainState_Late5)        { ProtossLate5(player);    }

    else if (mainState == e_mainState_Off)          { EndMeleeScript(player);  }
    else if (mainState == e_mainState_Disabled)     {                          }
    
    else { ErrorMeleeScript(player, "Invalid mainState"); }
}
